# NixOS Getting Started

For information about NFS, where to place your projects. see [this README](./README.md#storage).
TL;DR: Your home directory is a NFS share, which is backed up. Do not place VM images/projects/... there.
Use `/scratch/$USER` if you need fast, local disk access. This directory is not backed up.

## Packages

- **Where to look for packages:** https://search.nixos.org
- **Temporary vs. Global:** If you temporarily need a package, use `nix-shell -p package`.
If you need permanently installed packages for your user, consider [home-manager](#home-manager).

### Development Environment

**Flakes or Nix Shells:** Choose either format to define your project dependencies.

#### Flakes (recommended)

If you're inside a git project, `git add flake.nix`, as nix cannot find it otherwise. 

Example `flake.nix`:

```nix
{
  description = "An example dev shell.";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/24.05";

    utils.url = "github:numtide/flake-utils";
    utils.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = { self, nixpkgs, ... }@inputs: inputs.utils.lib.eachSystem [
    "x86_64-linux"
    "aarch64-linux"
    "x86_64-darwin"
    "aarch64-darwin"
  ]
    (system:
      let
        pkgs = import nixpkgs {
          inherit system;
        };
      in
      {
        devShell = pkgs.mkShell rec {
          name = "example dev shell";

          packages = with pkgs; [
            clang
          ];

          env = {
            # Your environment goes here
            MY_VAR = "Hello";
          };
        };
      });
}
```
- **Enter the dev shell:** Run `nix develop` or use direnv (see [later section](#direnv)).
- **Check-in the lockfile:** Commit the `flake.lock` generated by `nix develop` to guarantee reproducibility.


#### Nix Shells

Legacy option, still suitable for certain uses. Example:

```nix
with import <nixpkgs> { };
pkgs.llvmPackages_latest.stdenv.mkDerivation {
  name = "llvm-debug-env";
  nativeBuildInputs = [
    pkgs.clang
  ];
  MY_VAR = "Hello";
}
```
 **Enter the dev shell:** Run `nix-shell shell.nix` or use direnv (see later section).

### Direnv
**Automatic Setup:** Add `use flakes` or `use nix` to your `.envrc`.
This will automatically load/unload your development environment when you enter the directory containing it.
Add `.direnv` to your `.gitignore`.

## Home Manager

### Installation


```bash
nix-shell '<home-manager>' -A install
```

Installing home manager should create a `.config/home-manager/home.nix` that looks something like this:

```nix
{ config, pkgs, lib, ... }:
{
  # Let Home Manager install and manage itself.
  programs.home-manager.enable = true;

  # Home Manager needs a bit of information about you and the
  # paths it should manage.
  home.username = "<your name>";
  home.homeDirectory = "/home/<your name>";
  xdg.cacheHome = "/scratch/<your name>/.cache";
  xdg.stateHome = "/scratch/<your name>/.local/share";

  #direnv
  programs.direnv.enable = true;
  programs.direnv.nix-direnv.enable = true;

  # This value determines the Home Manager release that your
  # configuration is compatible with. This helps avoid breakage
  # when a new Home Manager release introduces backwards
  # incompatible changes.
  #
  # You can update Home Manager without changing this value. See
  # the Home Manager release notes for a list of state version
  # changes in each release.
  home.stateVersion = "24.05";

  home.packages = [
    pkgs.neovim-unwrapped
    pkgs.bat
    pkgs.gitFull
    pkgs.eza
    pkgs.clang-tools
    pkgs.fd
    pkgs.gnupg
  ];

  home.sessionVariables = {
    EDITOR = "nvim";
  };
}
```

- **Apply Changes:** Run `home-manager switch` to update your configuration.

